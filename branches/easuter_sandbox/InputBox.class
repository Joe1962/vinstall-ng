' Gambas class file

'''
' Name: InputBox
' Author: Timothy Marshal-Nichols
' Company: automationControls
' eMail: timothy@m-nichols.freeserve.co.uk
' Version: 1.0
' Version Date: Febuuary 2006
' Version History:
'
' TWEAKED BY JOE1962, easuter
'
'''
' Licence Information
'
' This program is free software; you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation; either version 2 of the License, or
' (at your option) any later version.
'
' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.
'
' You should have received a copy of the GNU General Public License
' along with this program; if not, write to the Free Software
' Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301  USA
'
' http://www.gnu.org/licenses/gpl.html
'
'''
' Description:
'
' An text input box. A replacement for the Visual Basic InputBox.
'
'''
' Dependencies:
'
' gb - Gambas internal native classes
' gb.qt - Graphical QT toolkit component
'
'''
' External Dependencies:
'
' None
'
'''
' Class Usage:
'
' Call the static method Input
' 
' InputBox.Input(Prompt [, Title] [, DefaultValue])
'
'   Prompt
'     A string message to display in the dialog box. This may contain HTML formatting.
'
'   Title
'     Optional title of the dialog box. If not set the the Application Title is used.
'
'   DefaultValue
'     Optional default string displayed in the text entry area. If not set then the text entry area is empty
'
'   Returns
'     Returns the string in the text entry area if the OK button is pressed.
'     Returns an empty string (“”) if the Cancel button is pressed
'
'   Examples:
'
'     ' Setting just the prompt. HTML formatting is used for the prompt.
'     DIM returnString AS String
'     returnString = InputBox.Input("Input String: <b>Bold Value</b><br>Another line of text")
'     IF returnString THEN
'       ' Do something with returnString
'     END IF
'
'     ' Setting the prompt and dialog title.
'     DIM returnString AS String
'     returnString = InputBox.Input("Input Value", "Show a new title")
'     IF returnString THEN
'       ' Do something with returnString
'     END IF
'
'     ' Setting the prompt and default value.
'     DIM returnString AS String
'     returnString = InputBox.Input("Input Value", "", "Set a default value")
'     IF returnString THEN
'       ' Do something with returnString
'     END IF
'
'''

STATIC returnValue AS String

'''
''' InputBox display method
'''

STATIC PUBLIC FUNCTION Input(Prompt AS String, OPTIONAL Title AS String, OPTIONAL DefaultValue AS String) AS String
  DIM box AS NEW InputBox
  ' Set input box prompt
  box.Prompt = Prompt
  ' Set input box title
  IF Title THEN
    box.Title = Title
  ELSE
    box.Title = Application.Title
  END IF
  ' Set default value
  IF DefaultValue THEN
    box.DefaultValue = DefaultValue
  ELSE
    box.DefaultValue = ""
  END IF
  ' Init return string
  returnValue = ""
  box.ShowDialog
  RETURN returnValue
END


'''
''' Properties
'''

' Read/Write the prompt
PUBLIC PROPERTY Prompt AS String


PRIVATE SUB Prompt_Write(Value AS String)
  TextLabelPrompt.Text = Value
END


PRIVATE FUNCTION Prompt_Read() AS String
  RETURN TextLabelPrompt.Text
END


' Read/Write the default value
PUBLIC PROPERTY DefaultValue AS String


PRIVATE SUB DefaultValue_Write(Value AS String)
  ' We use Clear and Insert because this moves the 
  ' cursor to the end of the default text
  TextBox1.Clear()
  TextBox1.Insert(Value)
    ' select default value string:
    TextBox1.SelectAll
END

PRIVATE FUNCTION DefaultValue_Read() AS String
  RETURN TextBox1.Text
END

PUBLIC SUB btnOK_Click()
  ' Set the value to be returned by the Input static method
  returnValue = TextBox1.Text
  ME.Close
END

PUBLIC SUB btnCancel_Click()
  ME.Close
END


PUBLIC SUB TextBox1_KeyPress()

' Check for ENTER key and run OK button.

END


''' End of InputBox '''


